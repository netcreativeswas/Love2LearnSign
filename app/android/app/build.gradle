plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace = "com.love2learnsign.app"
    compileSdk = 36
    ndkVersion = "27.0.12077973"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }

    // Supprimer les avertissements de compilation Java
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:none"
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin {
        jvmToolchain(17)
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.love2learnsign.app"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = 23
        // Explicit target SDK for Play Store compliance.
        // NOTE: Keep this aligned with current Google Play target API requirements.
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    def keystoreProperties = new Properties()
    def keystorePropertiesFile = rootProject.file("key.properties")
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    def hasReleaseKeystore = keystorePropertiesFile.exists() && keystoreProperties['storeFile']
    def isReleaseTask = gradle.startParameter.taskNames.any { it.toLowerCase().contains("release") }
    if (isReleaseTask && !hasReleaseKeystore) {
        throw new GradleException(
            "Release build requires android/key.properties (not committed). " +
            "Copy android/key.properties.example to android/key.properties and set storeFile/passwords."
        )
    }

    signingConfigs {
        release {
            if (hasReleaseKeystore) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source = "../.."
}

apply plugin: 'com.google.gms.google-services'
dependencies {
    // Existing implementation lines...
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.core:core-ktx:1.16.0"
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-storage'
}